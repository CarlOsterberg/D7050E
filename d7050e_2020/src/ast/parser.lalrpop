use std::str::FromStr;

use crate::ast::*;

grammar;

pub Expr: Box<Expr> = {
    Expr BoolAlgOp Elemen => Box::new(Expr::Infix(<>)),
    Elemen,
};

Elemen: Box<Expr> = {
    Elemen ExprOp Factor => Box::new(Expr::Infix(<>)),
    Factor,
};

Factor: Box<Expr> = {
    Factor FactorOp Term => Box::new(Expr::Infix(<>)),
    Term,
};

pub Term: Box<Expr> = {
    PrefixOp Term => Box::new(Expr::Prefix(<>)),
    Num => Box::new(Expr::Number(<>)),
    Var => Box::new(Expr::Variable(<>)),
    "(" <Expr> ")"
};

PrefixOp: Opcode = {
    "-" => Opcode::Negate,
    "!" => Opcode::Not,
}

BoolAlgOp: Opcode = {
    "<" => Opcode::Less,
    ">" => Opcode::Greater,
    "||" => Opcode::Or,
    "&&" => Opcode::And,
    "==" => Opcode::Equals,
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};
Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};

Var: String = {
    r"([a-z]|[A-Z])([a-z]|[A-Z]|[0-9]|_)*" => String::from_str(<>).unwrap()
};